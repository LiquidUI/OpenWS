// Generated by Synactive Designer Version 2, 11, 208, 0
// Description:Edited by UMANG       

if(_transaction == 'VA05') {

	if(<'P[/44]'>.isValid) del('P[/44]');	//Disp.variants
	if(<'P[/20]'>.isValid) del('P[/20]');	//Further sel.criteria
	if(<'P[/33]'>.isValid) del('P[/33]');	//Organizational data
	if(<'P[/34]'>.isValid) del('P[/34]');	//Partner function...

	pushbutton([TOOLBAR],'@2M@Back','/n');

	set('F[Sold-to party]','1460');
	set('F[Material]','100-300');
	z_va05_sorg = '1000';
	z_va05_dchnl = '10';
	
	inputfield( [4,1], "Distribution Channel", [4,22],{ "name":"z_va05_dchnl", "size":2});
	inputfield( [5,1], "Division", [5,22],{ "name":"z_va05_div", "size":2});
	inputfield( [3,1], "Sales Organization", [3,22],{ "name":"z_va05_sorg", "size":4});

	pos("F[VBCOM-MATNR]", [6,1],{ "triple":true});	//Material
	pos("F[VBCOM-BSTNK]", [7,1]);	//Purchase order no. text
	pos("F[VBCOM-BSTKD]", [7,22]);	//Purchase order no. value
	pos("G[AUFTRAGSDATEN]", [9,0]);	//Sales order data
	pos("G[BLOCK_SELEKTIONSUMFANG]", [12,0]);	//Selection criteria

	onUIEvents['Enter'] = {'fcode':'=ORGA', 'process':executeVA05};
	pushbutton([17,0],'@13@Search Orders','=ORGA',{'process':executeVA05, 'size':[2,25]});
}

function executeVA05() {
	set('V[z_va05_error]','');

	if(isBlank(z_va05_sorg)) {
		message('E: Sales Organization is required!');
		enter('?');
		goto SCRIPT_END;
	}	

	onscreen 'SAPMV75A.0102'
		set('F[VBCOM-VKORG]','&V[z_va05_sorg]');	//Sales Organization
		set('F[VBCOM-VTWEG]','&V[z_va05_dchnl]');	//Distribution Channel
		set('F[VBCOM-SPART]','&V[z_va05_div]');	//Division
		enter();

	onscreen 'SAPMSDYP.0010'
		set('V[z_va05_error]','&F[MESSTXT1]');
		enter();
		
	onscreen 'SAPMV75A.0102'
		enter('/12');
		
	onscreen 'SAPMV75A.0100'
		if(!isBlank(z_va05_error)) {
			message('E: ' + z_va05_error);
			enter('?');
			goto SCRIPT_END;
		} else {
			enter();
		}
	SCRIPT_END:{};
}